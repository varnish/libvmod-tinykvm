varnishtest "RISC-V Hello World Backend"

shell {
clang-10 -O1 -Wall -Wextra -target riscv32 -march=rv32imfd -ffreestanding -nostdlib -c ${testdir}/${testname}.cpp -o /tmp/${testname}.o
ld.lld-10 -Ttext=0x120000 --undefined=on_recv /tmp/${testname}.o -o /tmp/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import riscv;
	import std;

	backend default none;

	sub vcl_init {
		riscv.embed_tenants("""
			{
				"xpizza.com": {
					"filename": "/tmp/${testname}",
					"group": "test"
				}
			}
		""");
	}

	sub vcl_recv {
		if (!riscv.fork(req.http.Host, req.http.X-Debug)) {
			return (synth(403));
		}

		/* Call into VM */
		riscv.vcall(ON_REQUEST);

		if (riscv.want_result() == "backend") {
			set req.http.X-Backend-Func = riscv.result_value(1);
			set req.http.X-Backend-Arg  = riscv.result_value(2);
			if (riscv.result_value(0) == 0) {
				return (pass);
			} else {
				return (hash);
			}
		}

		return (synth(400));
	}

	sub vcl_backend_fetch {
		if (riscv.fork(bereq.http.Host, bereq.http.X-Debug)) {
			riscv.vcall(ON_BACKEND_FETCH);
			if (bereq.http.X-Backend-Func) {
				set bereq.backend = riscv.vm_backend(
						bereq.http.X-Backend-Func,
						bereq.http.X-Backend-Arg);
			}
		}
	}
} -start

client c1 {
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200
} -run
