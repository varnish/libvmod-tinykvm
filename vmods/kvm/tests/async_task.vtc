varnishtest "KVM Backend: Basic async task test"

shell {
cat >${tmpdir}/${testname}_storage.c <<-EOF
#include "kvm_api.h"
int storage_counter = 0;

void my_storage_task(void *arg)
{
	(void) arg;
	__sync_fetch_and_add(&storage_counter, 1);
}

void my_disallowed_storage_task(void *arg)
{
}

int main()
{
	sys_storage_allow(my_storage_task);

	wait_for_requests();
}
EOF
cat >${tmpdir}/${testname}.c <<-EOF
#include <string.h>
#include <stdio.h>
#include "kvm_api.h"
extern int storage_counter;
extern void my_storage_task(void *arg);
extern void my_disallowed_storage_task(void *arg);

static void on_get(const char *url, const char* arg)
{
	static const char ctype[] = "text/plain";

	if (strcmp(url, "/task") == 0) {
		async_storage_task(my_storage_task, NULL);
		backend_response(200, ctype, sizeof(ctype)-1, "OK", 2);
	}

	if (strcmp(url, "/task2") == 0) {
		async_storage_task(my_disallowed_storage_task, NULL);
		backend_response(200, ctype, sizeof(ctype)-1, "OK", 2);
	}

	char buffer[24];
	snprintf(buffer, sizeof(buffer),
		"Count: %d", storage_counter);

	backend_response(200, ctype, sizeof(ctype)-1, buffer, strlen(buffer));
}

int main()
{
	set_backend_get(on_get);
	wait_for_requests();
}
EOF
gcc -static -O2 -Wl,-Ttext-segment=0x44000000 ${tmpdir}/${testname}_storage.c -I${testdir} -o ${tmpdir}/${testname}_storage
objcopy -w --extract-symbol --strip-symbol=!*storage* --strip-symbol=* ${tmpdir}/${testname}_storage ${tmpdir}/${testname}_storage.syms
gcc -static -O2 -Wl,--just-symbols=${tmpdir}/${testname}_storage.syms ${tmpdir}/${testname}.c -I${testdir} -o ${tmpdir}/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;

	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""{
			"xpizza.com": {
				"filename": "${tmpdir}/${testname}",
				"group": "test",
				"storage": true
			}
		}""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
				bereq.http.Host,
				bereq.url);
	}
} -start

client c1 {
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 0"
	expect resp.status == 200
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 0"
	expect resp.status == 200

	txreq -url "/task" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "OK"
	expect resp.status == 200

	delay 0.5

	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 1"
	expect resp.status == 200
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 1"
	expect resp.status == 200

	txreq -url "/task" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "OK"
	expect resp.status == 200

	delay 0.5

	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 2"
	expect resp.status == 200
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Count: 2"
	expect resp.status == 200

	txreq -url "/task2" -hdr "Host: xpizza.com"
	rxresp
	expect resp.status == 500

} -run
