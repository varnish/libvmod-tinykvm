varnishtest "KVM Backend: Basic POST test"

shell {
cat >${tmpdir}/${testname}.c <<-EOF
#include "kvm_api.h"

static void on_get(const char *url)
{
	set_cacheable(0, 1.0f);
	backend_response_str(200, "text/plain", url);
}
static void on_post(const char *url, const char *data, size_t len)
{
	if (strcmp("Hello World", data) == 0)
		backend_response_str(200, "text/plain", "OK");
	else
		backend_response_str(401, "text/plain", "Not OK");
}

int main(int argc, char **argv)
{
	set_backend_get(on_get);
	set_backend_post(on_post);
	wait_for_requests();
}
EOF
gcc -static -O2 ${tmpdir}/${testname}.c -I${testdir} -o ${tmpdir}/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;
	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""{
			"xpizza.com": {
				"filename": "${tmpdir}/${testname}",
				"key": "",
				"group": "test"
			}
		}""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
				bereq.http.Host,
				bereq.url);
	}
} -start

client c1 {
	txreq -url "/" -req POST -hdr "Host: xpizza.com" -body "Hello World"
	rxresp
	expect resp.status == 200
	expect resp.body == "OK"
} -run
