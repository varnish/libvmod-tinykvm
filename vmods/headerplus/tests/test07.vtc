varnishtest "Test headerplus.get*()"

varnish v1 -arg "-p workspace_client=256k" -vcl+backend {
	import headerplus;

	backend default none;

	sub vcl_recv {
		return (synth(200));
	}

	sub vcl_synth {
		headerplus.init(req);
		headerplus.add("test", "name2");

		set resp.http.v1 = headerplus.get("test");
		set resp.http.v2 = headerplus.get("test", "name\d");
		set resp.http.v3 = headerplus.get("test", "xxxxxd");
		set resp.http.v4 = headerplus.get("hoST");
		set resp.http.v5 = headerplus.get("notfound");
		set resp.http.v6 = headerplus.get(req.http.null);
		
		set resp.http.v7 = headerplus.get_regex("X-");
		set resp.http.v8 = headerplus.get_regex("X-", "xxx");
		set resp.http.v9 = headerplus.get_regex("X-", "127\.");
		set resp.http.v10 = headerplus.get_regex(".", "\dname");
		set resp.http.v11 = headerplus.get_regex("xxx");

		set resp.http.v18 = headerplus.get_name_regex();
		set resp.http.v19 = headerplus.get_name_regex("C");
		set resp.http.v20 = headerplus.get_name_regex("C", "value");
		set resp.http.v21 = headerplus.get_name_regex(value_re = "public");
		set resp.http.v22 = headerplus.get_name_regex("C", "xxx");
	}

} -start

client c1 {
	txreq -hdr "test: name-" -hdr "test: namex" -hdr "test: 1name" \
	-hdr "test: 2name" -hdr "Cache-Control: public, max-age=31536000" \
	-hdr "Contents: value1*1111; value2*23; value3*3"
	rxresp
	expect resp.http.v1 == "name-"
	expect resp.http.v2 == "name2"
	expect resp.http.v3 == ""
	expect resp.http.v4 == "127.0.0.1"
	expect resp.http.v5 == ""
	expect resp.http.v6 == ""

	expect resp.http.v7 == "127.0.0.1"
	expect resp.http.v8 == ""
	expect resp.http.v9 == "127.0.0.1"
	expect resp.http.v10 == "1name"
	expect resp.http.v11 == ""

	expect resp.http.v18 == "test"
	expect resp.http.v19 == "Cache-Control"
	expect resp.http.v20 == "Contents"
	expect resp.http.v21 == "Cache-Control"
	expect resp.http.v22 == ""
} -run
