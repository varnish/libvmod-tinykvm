varnishtest "Test headerplus.keep/delete/count*()"

server s1 {
	rxreq
	txresp -hdr "resp1: 1" -hdr "resp2: 2" -hdr "respX: 3" -hdr "X-test: 1" \
	-hdr "X-test3: 2"  -hdr "X-test4: 2" -hdr "R: 1" -hdr "R5: 3" -hdr "R6: 3"
} -start

varnish v1 -arg "-p workspace_client=256k" -vcl+backend {
	import headerplus;

	sub vcl_deliver {
		headerplus.init(req);
		set resp.http.v1 = headerplus.count_regex(".");
		headerplus.keep("test");
		set resp.http.v2 = headerplus.count_regex(".");
		set resp.http.v3 = headerplus.count_regex("C");
		set resp.http.v4 = headerplus.count("Cache-Control");
		headerplus.keep_regex(".");
		set resp.http.v5 = headerplus.count_regex(".");

		headerplus.init(req);
		headerplus.keep_regex("C");
		set resp.http.v6 = headerplus.count_regex(".");
		set resp.http.v7 = headerplus.count("test");
		headerplus.keep("test");
		set resp.http.v8 = headerplus.count_regex(".");

		headerplus.init(req);
		set resp.http.v9 = headerplus.count_regex(".");
		headerplus.delete("test");
		set resp.http.v10 = headerplus.count_regex(".");
		set resp.http.v11 = headerplus.count("test");
		set resp.http.v12 = headerplus.count_regex("C");
		headerplus.delete_regex("C");
		set resp.http.v13 = headerplus.count_regex("C");
		set resp.http.v14 = headerplus.count_regex(".");

		headerplus.init(resp);
		# Only count headers from the backend
		set resp.http.v15 = headerplus.count_regex("^[^v]");
		headerplus.keep_regex("resp\d");
		headerplus.keep_regex("X-test");
		headerplus.delete_regex("X-test\d", true);
		headerplus.keep("R");
		headerplus.keep("R5");
		set resp.http.v16 = headerplus.count_regex("R");
		headerplus.delete("resp2", true);
		set resp.http.v17 = headerplus.count_regex(".");
		headerplus.keep_regex("v");
		set resp.http.v18 = headerplus.count("X");
		set resp.http.v19 = headerplus.count_regex("X");
		headerplus.delete_regex("X", false);
		set resp.http.v20 =  headerplus.count_regex("X");
		headerplus.write();
	}

} -start

client c1 {
	txreq -hdr "test: name-" -hdr "test: namex" -hdr "test: 1name" \
	-hdr "test: 2name" -hdr "Cache-Control: public, max-age=31536000" \
	-hdr "Contents: value1*1; value2*2; value3*3"
	rxresp
	expect resp.http.v1 == 8
	expect resp.http.v2 == 4
	expect resp.http.v3 == 0
	expect resp.http.v4 == 0
	expect resp.http.v5 == resp.http.v1
	expect resp.http.v6 == 2
	expect resp.http.v7 == 0
	expect resp.http.v8 == 6
	expect resp.http.v9 == resp.http.v1
	expect resp.http.v10 == 4
	expect resp.http.v11 == 0
	expect resp.http.v12 == 2
	expect resp.http.v13 == 0
	expect resp.http.v14 == 2
	expect resp.http.v15 == 14
	expect resp.http.v16 == 2
	expect resp.http.v17 == 4
	expect resp.http.v18 == 0
	expect resp.http.v19 == 1
	expect resp.http.v20 == resp.http.v19

	expect resp.http.resp1 == 1
	expect resp.http.resp2 == <undef>
	expect resp.http.respx == <undef>
	expect resp.http.X-test == 1
	expect resp.http.X-test3 == <undef>
	expect resp.http.X-test4 == <undef>
	expect resp.http.r == 1
	expect resp.http.r5 == 3
	expect resp.http.r6 == <undef>
	

} -run
