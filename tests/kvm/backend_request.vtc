varnishtest "KVM Backend: HTTP request test"

shell {
cat >${tmpdir}/${testname}.c <<-EOF
#include "kvm_api.h"
#include <string.h>

static void on_request(const struct backend_request *req)
{
	if (strcmp(req->url, "/1") == 0)
	{
		if (strcmp(req->method, "TEST") == 0
			&& strcmp(req->content_type, "") == 0)
			backend_response_str(200, "text/plain", "Hello World");
	}
	else if (strcmp(req->url, "/2") == 0)
	{
		if (strcmp(req->method, "TEST") == 0
			&& strcmp(req->content_type, "text/plain") == 0
			&& memcmp(req->data, "Hello World!", req->datalen) == 0
		)
			backend_response_str(200, "text/plain", "Hello World");
	}

	backend_response_str(500, "text/plain", "Failed");
}

int main(int argc, char **argv)
{
	set_backend_request(on_request);
	wait_for_requests();
}
EOF
gcc -static -O2 ${tmpdir}/${testname}.c -I${testdir} -o ${tmpdir}/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;
	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""{
			"test.com": {
				"filename": "${tmpdir}/${testname}",
				"group": "test"
			}
		}""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
			bereq.http.Host,
			bereq.url);
	}
} -start

client c1 {
	txreq -req TEST -url "/1" -hdr "Host: test.com" -hdr "Content-Type: text/plain"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200

	txreq -req TEST -url "/2" -hdr "Host: test.com" -hdr "Content-Type: text/plain" -body "Hello World!"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200

	txreq -req TEST -url "/2" -hdr "Host: test.com" -hdr "Content-Type: text/plain2" -body "Hello World!"
	rxresp
	expect resp.status == 500

	txreq -req TEST -url "/2" -hdr "Host: test.com" -hdr "Content-Type: text/plain" -body "Hello World!2"
	rxresp
	expect resp.status == 500
} -run
