varnishtest "KVM Backend: Tenant initialization"

shell {
cat >${tmpdir}/${testname}.c <<-EOF
#include "kvm_api.h"

int main(int argc, char **argv)
{
	void on_get(const char *url) {
		set_cacheable(0, 1.0f, 0.0f, 0.0f);
		backend_response_str(200, "text/plain", "Hello World");
	}
	set_backend_get(on_get);
	wait_for_requests();
}
EOF
gcc -static -O2 ${tmpdir}/${testname}.c -I${testdir} -o ${tmpdir}/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;
	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""{
			"xpizza.com": {
				"filename": "${tmpdir}/${testname}",
				"key": "",
				"group": "test"
			}
		}""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
				bereq.http.Host, bereq.url);
	}
} -start

client c1 {
	txreq -url "/my_backend" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200
	txreq -url "/my_backend" -hdr "Host: xpizza.com"
	rxresp
	expect resp.body == "Hello World"
	expect resp.status == 200
} -run
