varnishtest "KVM Backend: Empty or missing program returns 503"

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;

	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""
			{
				"xpizza.com": {
					"filename": "/tmp/nothing",
					"key": "",
					"group": "test"
				},
				"ypizza.com": {
					"filename": "/dev/zero",
					"key": "",
					"group": "test"
				}
			}
		""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
			bereq.http.Host, bereq.url);
	}
} -start

client c1 {
	txreq -url "/" -hdr "Host: xpizza.com"
	rxresp
	expect resp.status == 503
	txreq -url "/" -hdr "Host: ypizza.com"
	rxresp
	expect resp.status == 503
	txreq -url "/" -hdr "Host: zpizza.com"
	rxresp
	expect resp.status == 503
} -run
