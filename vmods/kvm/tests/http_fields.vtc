varnishtest "KVM Backend: Basic HTTP test"

shell {
cat >${tmpdir}/${testname}.c <<-EOF
#include <stdio.h>
#include <string.h>
#include "kvm_api.h"

static void my_backend(const char *url, const char *arg2, int req, int resp)
{
	http_appendf(resp, "X-Value: 5", 10);
	http_setf(resp, "X-OtherValue: 6", 15);

	/* Test overwriting a header field. */
	if (strcasecmp(url, "/test2") == 0) {
		http_setf(resp, "X-OtherValue: 7", 15);
	}

	backend_response_str(200, "text/plain", url);
}

int main()
{
	set_backend_get(my_backend);
	wait_for_requests();
}
EOF
gcc -static -O2 ${tmpdir}/${testname}.c -I${testdir} -o ${tmpdir}/${testname}
}

varnish v1 -vcl+backend {
vcl 4.1;
	import kvm;

	backend default none;

	sub vcl_init {
		kvm.embed_tenants("""{
			"xpizza.com": {
				"filename": "${tmpdir}/${testname}",
				"key": "",
				"group": "test"
			}
		}""");
	}

	sub vcl_recv {
		return (pass);
	}

	sub vcl_backend_fetch {
		set bereq.backend = kvm.vm_backend(
				bereq.http.Host,
				bereq.url);
	}
} -start

client c1 {
	txreq -url "/test1" -hdr "Host: xpizza.com"
	rxresp
	expect resp.http.X-Value == "5"
	expect resp.http.X-OtherValue == "6"
	expect resp.body == "/test1"
	expect resp.status == 200
	txreq -url "/test2" -hdr "Host: xpizza.com"
	rxresp
	expect resp.http.X-Value == "5"
	expect resp.http.X-OtherValue == "7"
	expect resp.body == "/test2"
	expect resp.status == 200
} -run
